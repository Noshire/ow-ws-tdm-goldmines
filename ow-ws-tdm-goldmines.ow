settings
{
	main
	{
		Description: "Drain Energy from Rifts around the \r\nbattlefield and return it to your base.\r\nDefeat your foes to steal their Energy!"
		Mode Name: "Rift Raiders"
	}

	lobby
	{
		Map Rotation: Paused
		Max Team 1 Players: 3
		Max Team 2 Players: 3
	}

	modes
	{
		Team Deathmatch
		{
			enabled maps
			{
				Ilios Lighthouse
			}
		}

		General
		{
			Game Mode Start: Immediately
			Self Initiated Respawn: Off
		}
	}

	heroes
	{
		General
		{
			disabled heroes
			{
				Sombra
			}
		}
	}

	workshop
	{
		Debug Mode: On
	}

	extensions
	{
		Beam Effects
		Beam Sounds
		Buff Status Effects
		Debuff Status Effects
		Buff and Debuff Sounds
		Energy Explosion Effects
		Kinetic Explosion Effects
		Explosion Sounds
		Play More Effects
	}
}

variables
{
	global:
		26: SpawnPositionTeamArray
		27: MinePositionArray
		28: ArrayIteratorA
		29: MineGoldMax
		30: MineGoldCountArray
		31: MineRadius
		32: MineRespawnDuration
		33: MineRespawnTimerArray
		34: RespawningMineColor
		35: ArrayIteratorB
		36: DEBUG
		37: MinePlayerIntersectionArray2D
		38: RadiusPlayerProximityBuffer
		39: PlayerGoldHarvestInterval
		40: PlayerGoldHarvestAmountBase
		41: ScoreTeamArray
		42: ScoreLimit
		43: ColorTeamArray
		44: PlayerEnterBaseTimerMax
		45: TeamArray
		46: SpawnRadius
		47: GoldColor
		48: PlayerDeathGoldDropRatio
		49: GoldLootOrbPositionArray
		50: GoldLootOrbValueArray
		51: GoldLootOrbEffectArray
		52: GoldLootOrbCollectionRadius
		53: GoldLootOrbVisualRadius
		54: GoldLootOrbSpawnQueue
		55: UpdateInterval
		56: PlayerGoldDepositPerFrame
		57: BaseColor
		58: GoldName
		59: GoldColorHarvesting
		60: GoldColorExhausted
		61: GoldCarrySpeedPenaltyAmount
		62: GoldCarrySpeedPenaltyInterval
		63: TeamGoldHarvestAmountFactorArray
		64: EnableGoldHarvestBalancing
		65: GoldCarrySpeedPenaltyMax
		66: GoldLootOrbTextArray
		67: SubroutineTargetA
		68: GoldCarrySpeedPenaltyFreeAmount

	player:
		26: TeamIndex
		27: ClosestHarvestingMine
		28: HUDArray
		29: GoldCount
		30: GoldHarvestAmount
		31: EnterBaseTimer
		32: ArrayIteratorA
		33: IsInBase
		34: GoldDepositSoundEffectId
		35: UltimateCharge
		36: IsViewingTutorial
		37: EffectArray
		38: SpeedPenalty
}

subroutines
{
	0: CalculateTeamHandicap
	1: CalculatePlayerSpeedPenalty
}

rule("DEBUG:VoiceUp@Player: SpawnDummyBotSameTeam")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Global.DEBUG == True;
		Is Communicating(Event Player, Voice Line Up) == True;
	}

	actions
	{
		Create Dummy Bot(Random Value In Array(All Heroes), Team Of(Event Player), -1,
			Global.SpawnPositionTeamArray[Event Player.TeamIndex], Vector(0, 0, 0));
	}
}

rule("DEBUG:VoiceDown@Player: SpawnDummyBotOtherTeam")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Global.DEBUG == True;
		Is Communicating(Event Player, Voice Line Down) == True;
	}

	actions
	{
		Create Dummy Bot(Random Value In Array(All Heroes), Opposite Team Of(Team Of(Event Player)), -1,
			Global.SpawnPositionTeamArray[1 - Event Player.TeamIndex], Vector(0, 0, 0));
	}
}

rule("DEBUG:VoiceRight@Player: DestroyAllDummyBots")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Global.DEBUG == True;
		Is Communicating(Event Player, Voice Line Right) == True;
	}

	actions
	{
		Destroy All Dummy Bots;
	}
}

rule("DEBUG:Interact@Player: TeleportToRandomMine")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Global.DEBUG == True;
		Has Spawned(Event Player) == True;
		Is Alive(Event Player) == True;
		Is Button Held(Event Player, Button(Interact)) == True;
	}

	actions
	{
		Teleport(Event Player, Random Value In Array(Global.MinePositionArray));
	}
}

rule("DEBUG:Init@Global: StartImmediately")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Global.DEBUG == True;
	}

	actions
	{
		Set Match Time(6);
	}
}

rule("Init@Global: ApplyWorkshopSettings: General")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.ScoreLimit = Workshop Setting Integer(Custom String("General"), Custom String("Score Limit"), 3000, 10, 100000, 0);
		Global.SpawnRadius = Workshop Setting Real(Custom String("General"), Custom String("Team Base Radius"), 5, 0.500, 15, 0);
		Global.GoldLootOrbCollectionRadius = Workshop Setting Real(Custom String("General"), Custom String("Death Loot Collection Radius"),
			3, 0.500, 15, 0);
		Global.GoldLootOrbVisualRadius = Workshop Setting Real(Custom String("General"), Custom String("Death Loot Visual Radius"), 0.350,
			0.100, 15, 0);
		Global.EnableGoldHarvestBalancing = Workshop Setting Toggle(Custom String("General"), Custom String(
			"Gold Harvest Auto-Balancing (smaller teams harvest faster)"), True, 0);
	}
}

rule("Init@Global: ApplyWorkshopSettings: Players")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.PlayerGoldHarvestInterval = Workshop Setting Real(Custom String("Players"), Custom String(
			"Gold Harvest Interval in Seconds"), 0.500, 0.050, 60, 0);
		Global.PlayerGoldHarvestAmountBase = Workshop Setting Integer(Custom String("Players"), Custom String("Gold Harvest Amount Base"),
			10, 0, 1000, 1);
		Global.PlayerEnterBaseTimerMax = Workshop Setting Real(Custom String("Players"), Custom String("Gold Deposit Timer in Seconds"),
			3.500, 0, 60, 2);
		Global.PlayerDeathGoldDropRatio = Workshop Setting Real(Custom String("Players"), Custom String("Gold Drop Ratio"), 1, 0, 1, 3);
		Global.PlayerGoldDepositPerFrame = Workshop Setting Integer(Custom String("Player"), Custom String("Gold Deposit Speed"), 5, 1,
			100000, 4);
		Global.GoldCarrySpeedPenaltyAmount = Workshop Setting Real(Custom String("Player"), Custom String(
			"Movement speed penalty % per X Energy carried"), 5, 0, 100, 5);
		Global.GoldCarrySpeedPenaltyInterval = Workshop Setting Real(Custom String("Player"), Custom String(
			"Amount of Energy per movement speed decrease step"), 100, 1, 100000, 6);
		Global.GoldCarrySpeedPenaltyMax = Workshop Setting Real(Custom String("Player"), Custom String("Maximum movement speed penalty %"),
			35, 0, 100, 7);
		Global.GoldCarrySpeedPenaltyFreeAmount = Workshop Setting Real(Custom String("Player"), Custom String(
			"Penalty-free amount of Energy"), 200, 0, 100000, 8);
	}
}

rule("Init@Global: ApplyWorkshopSettings: Gold Mines")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.MineGoldMax = Workshop Setting Integer(Custom String("Gold Mines"), Custom String("Max. Gold per Mine"), 200, 10, 1000, 0);
		Global.MineRadius = Workshop Setting Real(Custom String("Gold Mines"), Custom String("Radius"), 2.500, 0.500, 15, 0);
		Global.MineRespawnDuration = Workshop Setting Real(Custom String("Gold Mines"), Custom String("Regeneration Duration in Seconds"),
			20, 1, 300, 0);
	}
}

rule("Init@Global: ApplyWorkshopSettings: Dev")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.DEBUG = Workshop Setting Toggle(Custom String("Dev Tools"), Custom String("Debug Mode"), False, 0);
	}
}

rule("Init@Global: Map: Ilios Lighthouse")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Current Map == Map(Ilios Lighthouse);
	}

	actions
	{
		Global.SpawnPositionTeamArray = Array(Vector(336.461, -24.274, 22.927), Vector(290.193, -24.276, -88.772));
		Global.MinePositionArray = Array();
		"Red: Balcony"
		Modify Global Variable(MinePositionArray, Append To Array, Vector(329.230, -17.341, -56.517));
		"Blue: Balcony"
		Modify Global Variable(MinePositionArray, Append To Array, Vector(341.309, -17.341, -27.567));
		"Red: Terrace"
		disabled Modify Global Variable(MinePositionArray, Append To Array, Vector(332.087, -26.341, -95.429));
		"Blue: Terrace"
		disabled Modify Global Variable(MinePositionArray, Append To Array, Vector(370.586, -26.341, -1.469));
		"Red: Street Corner"
		disabled Modify Global Variable(MinePositionArray, Append To Array, Vector(307.874, -22.341, -43.050));
		"Blue: Street Corner"
		disabled Modify Global Variable(MinePositionArray, Append To Array, Vector(317.227, -22.342, -22.655));
		"Neutral: Lighthouse"
		Modify Global Variable(MinePositionArray, Append To Array, Vector(357.681, -24.341, -51.111));
		"Neutral: Central Building"
		Modify Global Variable(MinePositionArray, Append To Array, Vector(327.378, -22.287, -38.788));
	}
}

rule("Init@Global: InitGlobalVars")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.TeamArray = Array(Team 1, Team 2);
		Global.ScoreTeamArray = Array(0, 0);
		Global.RadiusPlayerProximityBuffer = 0.500;
		Global.GoldColor = Custom Color(206, 23, 209, 255);
		Global.GoldColorHarvesting = Custom Color(232, 187, 102, 255);
		Global.GoldColorExhausted = Custom Color(0, 0, 0, 0);
		Global.ColorTeamArray = Array(Custom Color(39, 60, 219, 255), Custom Color(219, 39, 39, 255));
		Global.BaseColor = Custom Color(125, 255, 251, 255);
		Global.RespawningMineColor = Custom Color(18, 18, 18, 128);
		Global.UpdateInterval = 0.016;
		Call Subroutine(CalculateTeamHandicap);
		Global.GoldName = Custom String("Energy");
	}
}

rule("Init@Global: InitGameMode")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Disable Built-In Game Mode Completion;
		Disable Built-In Game Mode Scoring;
		Pause Match Time;
	}
}

rule("Init@Global: InitMines")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		For Global Variable(ArrayIteratorA, 0, Count Of(Global.MinePositionArray), 1);
			Log To Inspector(Custom String("Mine at: {0}", Global.MinePositionArray[Global.ArrayIteratorA]));
			Global.ArrayIteratorB = Global.MineGoldCountArray[Global.ArrayIteratorA];
			Global.MineRespawnTimerArray[Global.ArrayIteratorA] = 0;
			Global.MineGoldCountArray[Global.ArrayIteratorA] = Global.MineGoldMax;
			Create Effect(All Players(All Teams), Sphere, Global.MineGoldCountArray[Evaluate Once(Global.ArrayIteratorA)
				] == 0 ? Global.GoldColorExhausted : (Count Of(Global.MinePlayerIntersectionArray2D[Evaluate Once(Global.ArrayIteratorA)])
				== 0 ? Global.GoldColor : Global.GoldColorHarvesting), Global.MinePositionArray[Global.ArrayIteratorA], Global.MineRadius,
				Visible To and Color);
			Create Beam Effect(Count Of(Global.MinePlayerIntersectionArray2D[Evaluate Once(Global.ArrayIteratorA)]) > 0 ? All Players(
				All Teams) : Null, Moira Coalescence Beam, Global.MinePositionArray[Global.ArrayIteratorA] + Up * 100,
				Global.MinePositionArray[Global.ArrayIteratorA] + Up * Global.MineRadius, Global.GoldColor, Visible To);
			Create Progress Bar In-World Text(Filtered Array(All Players(All Teams), !Array Contains(
				Global.MinePlayerIntersectionArray2D[Evaluate Once(Global.ArrayIteratorA)], Current Array Element)),
				Global.MineGoldCountArray[Evaluate Once(Global.ArrayIteratorA)] > 0 ? 100 * Global.MineGoldCountArray[Evaluate Once(
				Global.ArrayIteratorA)] / Global.MineGoldMax : 100 * Global.MineRespawnTimerArray[Evaluate Once(Global.ArrayIteratorA)
				] / Global.MineRespawnDuration, Global.MineGoldCountArray[Evaluate Once(Global.ArrayIteratorA)] > 0 ? Custom String("{0}/{1}",
				Global.MineGoldCountArray[Evaluate Once(Global.ArrayIteratorA)], Global.MineGoldMax) : Custom String("Regenerating..."),
				Global.MinePositionArray[Global.ArrayIteratorA] + Up * 2.500, 0.350, Clip Against Surfaces,
				Global.MineGoldCountArray[Evaluate Once(Global.ArrayIteratorA)] > 0 ? Global.GoldColor : Global.RespawningMineColor,
				Global.MineGoldCountArray[Evaluate Once(Global.ArrayIteratorA)] > 0 ? Global.GoldColor : Global.RespawningMineColor,
				Visible To Values and Color, Default Visibility);
		End;
	}
}

rule("Init@Global: InitSpawns")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		For Global Variable(ArrayIteratorA, 0, Count Of(Global.SpawnPositionTeamArray), 1);
			Log To Inspector(Custom String("Spawn at: {0}", Global.SpawnPositionTeamArray[Global.ArrayIteratorA]));
			Create Effect(All Players(All Teams), Sphere, Global.ColorTeamArray[Global.ArrayIteratorA],
				Global.SpawnPositionTeamArray[Global.ArrayIteratorA], Global.SpawnRadius, Visible To);
		End;
	}
}

rule("Init@Player: InitPlayerVars")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	actions
	{
		Event Player.GoldHarvestAmount = Global.PlayerGoldHarvestAmountBase;
		Event Player.GoldCount = 0;
		disabled Event Player.IsInBase = True;
	}
}

rule("Init@Global: HUD")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Create Progress Bar HUD Text(All Players(All Teams), 100 * Min(1, Global.ScoreTeamArray[0] / Global.ScoreLimit), Custom String(
			"Blue Team: {0} / {1}", Global.ScoreTeamArray[0], Global.ScoreLimit), Left, 0, Global.ColorTeamArray[0],
			Global.ColorTeamArray[0], Visible To and Values, Visible Always);
		Create Progress Bar HUD Text(All Players(All Teams), 100 * Min(1, Global.ScoreTeamArray[1] / Global.ScoreLimit), Custom String(
			"Red Team: {0} / {1}", Global.ScoreTeamArray[1], Global.ScoreLimit), Right, 0, Global.ColorTeamArray[1],
			Global.ColorTeamArray[1], Visible To and Values, Visible Always);
		Create In-World Text(All Players(Team 1), Custom String("{0} Base", Icon String(Flag)),
			Global.SpawnPositionTeamArray[0] + Up * 2.500, 1.500, Do Not Clip, Visible To, Global.ColorTeamArray[0], Default Visibility);
		Create In-World Text(All Players(Team 2), Custom String("{0} Base", Icon String(Flag)),
			Global.SpawnPositionTeamArray[1] + Up * 2.500, 1.500, Do Not Clip, Visible To, Global.ColorTeamArray[1], Default Visibility);
	}
}

rule("Init@Player: SetTeamIndex")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	actions
	{
		Event Player.TeamIndex = Team Of(Event Player) == Team 1 ? 0 : 1;
	}
}

rule("Init@Player: HUD")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Disable Game Mode HUD(Event Player);
		"Clean up any previous HUD texts, e.g. after swapping teams."
		For Player Variable(Event Player, ArrayIteratorA, 0, Count Of(Event Player.HUDArray), 1);
			Destroy HUD Text(Event Player.HUDArray[Event Player.ArrayIteratorA]);
		End;
		Event Player.HUDArray = Array();
		Create HUD Text(Event Player.IsViewingTutorial == False ? Event Player : Null, Custom String("{0} {1}", Event Player.GoldCount,
			Global.GoldName), Null, Null, Top, 0, Global.GoldColor, Color(White), Color(White), Visible To and String, Default Visibility);
		Modify Player Variable(Event Player, HUDArray, Append To Array, Last Text ID);
		Create Progress Bar HUD Text(Event Player.EnterBaseTimer > 0 && Event Player.IsViewingTutorial == False ? Event Player : Null,
			100 * Min(1, Event Player.EnterBaseTimer / Global.PlayerEnterBaseTimerMax), Custom String("Entering Base..."), Top, 1,
			Global.BaseColor, Global.BaseColor, Visible To Values and Color, Default Visibility);
		Modify Player Variable(Event Player, HUDArray, Append To Array, Last Text ID);
		Create Progress Bar HUD Text(
			Event Player.ClosestHarvestingMine > -1 && Event Player.IsViewingTutorial == False ? Event Player : Null, 100 * Min(1,
			Global.MineGoldCountArray[Event Player.ClosestHarvestingMine] / Global.MineGoldMax), Custom String("{0} / {1}",
			Global.MineGoldCountArray[Event Player.ClosestHarvestingMine], Global.MineGoldMax), Top, 1, Global.GoldColor, Global.GoldColor,
			Visible To and Values, Default Visibility);
		Modify Player Variable(Event Player, HUDArray, Append To Array, Last Text ID);
		Create HUD Text(Event Player.IsInBase == True && Event Player.IsViewingTutorial == False ? Event Player : Null, Custom String(
			"{0} IN BASE {0}", Ability Icon String(Hero(Winston), Button(Ability 2))), Null, Null, Top, 2, Global.BaseColor,
			Global.BaseColor, Global.BaseColor, Visible To, Default Visibility);
		Modify Player Variable(Event Player, HUDArray, Append To Array, Last Text ID);
		Create HUD Text(Event Player.IsInBase == True && Event Player.IsViewingTutorial == False ? Event Player : Null, Null,
			Custom String("Crouch for Tutorial"), Null, Top, 3, Global.BaseColor, Global.BaseColor, Global.BaseColor, Visible To,
			Default Visibility);
		Modify Player Variable(Event Player, HUDArray, Append To Array, Last Text ID);
		Create HUD Text(Event Player.SpeedPenalty > 0 && Event Player.IsViewingTutorial == False ? Event Player : Null, Null,
			Custom String("Overburdened! -{0}% Speed.", Event Player.SpeedPenalty), Null, Top, 4, Global.BaseColor, Global.BaseColor,
			Global.BaseColor, Visible To and String, Default Visibility);
		Modify Player Variable(Event Player, HUDArray, Append To Array, Last Text ID);
	}
}

rule("Init@Player: Effects")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		"Clean up any previous HUD texts, e.g. after swapping teams."
		For Player Variable(Event Player, ArrayIteratorA, 0, Count Of(Event Player.EffectArray), 1);
			Destroy Effect(Event Player.EffectArray[Event Player.ArrayIteratorA]);
		End;
		Event Player.EffectArray = Array();
		Create Effect(Event Player.IsInBase == True ? All Players(All Teams) : Null, Echo Cloning Effect, Custom Color(150, 150, 150, 150),
			Event Player, 3, Visible To Position and Radius);
		Modify Player Variable(Event Player, EffectArray, Append To Array, Last Created Entity);
		Create Effect(Event Player.IsInBase == True ? All Players(All Teams) : Null, LÃºcio Sound Barrier Protected Effect, Custom Color(
			150, 150, 150, 150), Event Player, 3, Visible To Position and Radius);
		Modify Player Variable(Event Player, EffectArray, Append To Array, Last Created Entity);
	}
}

rule("Update@Global: UpdateMineRespawn")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Wait(Global.UpdateInterval, Abort When False);
		For Global Variable(ArrayIteratorA, 0, Count Of(Global.MineRespawnTimerArray), 1);
			If(Global.MineGoldCountArray[Global.ArrayIteratorA] <= 0);
				Global.MineRespawnTimerArray[Global.ArrayIteratorA] = Min(Global.MineRespawnDuration,
					Global.MineRespawnTimerArray[Global.ArrayIteratorA] + Global.UpdateInterval);
				"If timer is complete, refill the gold."
				If(Global.MineRespawnTimerArray[Global.ArrayIteratorA] >= Global.MineRespawnDuration);
					Global.MineRespawnTimerArray[Global.ArrayIteratorA] = 0;
					Global.MineGoldCountArray[Global.ArrayIteratorA] = Global.MineGoldMax;
				End;
			End;
		End;
		Loop If Condition Is True;
	}
}

rule("Update@Global: UpdateMinePlayerIntersection")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Wait(0.100, Abort When False);
		"Clear the \"closest GoldMine in range\" for all players."
		For Global Variable(ArrayIteratorA, 0, Count Of(All Players(All Teams)), 1);
			All Players(All Teams)[Global.ArrayIteratorA].ClosestHarvestingMine = -1;
		End;
		"Loop through all GoldMines..."
		For Global Variable(ArrayIteratorA, 0, Count Of(Global.MinePositionArray), 1);
			"Update the list of players within range of this GoldMine."
			Global.MinePlayerIntersectionArray2D[Global.ArrayIteratorA] = Filtered Array(All Players(All Teams), Has Spawned(
				Current Array Element) && Is Alive(Current Array Element)
				&& Global.MineGoldCountArray[Global.ArrayIteratorA] > 0 && Distance Between(Global.MinePositionArray[Global.ArrayIteratorA],
				Position Of(Current Array Element)) <= Global.MineRadius + Global.RadiusPlayerProximityBuffer);
			"Loop through all players in range of this GoldMine..."
			For Global Variable(ArrayIteratorB, 0, Count Of(Global.MinePlayerIntersectionArray2D[Global.ArrayIteratorA]), 1);
				disabled Log To Inspector(Custom String("CheckDist: GoldMine {0}, Player {1}.", Global.ArrayIteratorA, Global.ArrayIteratorB));
				"Cache the Player to a temporary variable."
				Global.A = Global.MinePlayerIntersectionArray2D[Global.ArrayIteratorA][Global.ArrayIteratorB];
				Global.B = Distance Between(Position Of(Global.A), Global.MinePositionArray[Global.ArrayIteratorA]);
				Global.C = Global.A.ClosestHarvestingMine < 0 ? 99999 : Distance Between(Position Of(Global.A),
					Global.MinePositionArray[Global.A.ClosestHarvestingMine]);
				disabled Log To Inspector(Custom String("P: {0}, NextDist: {1}, MinDist: {2}", Global.A, Global.B, Custom String("{0}, ClosestMine: {1}",
					Global.C, Global.A.ClosestHarvestingMine)));
				"If the distance is less than the previous smallest distance between the player and a GoldMine..."
				If(Global.B < Global.C);
					"Set it as the closest GoldMine for this player."
					Global.A.ClosestHarvestingMine = Evaluate Once(Global.ArrayIteratorA);
					disabled Log To Inspector(Custom String("Set Closest Mine: Player: {0}, Mine: {1}", Global.A, Global.A.ClosestHarvestingMine));
				End;
			End;
		End;
		Loop If Condition Is True;
	}
}

rule("Update@Global: UpdateMinePlayerHarvest")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Wait(Global.PlayerGoldHarvestInterval, Ignore Condition);
		For Global Variable(ArrayIteratorA, 0, Count Of(Global.MinePlayerIntersectionArray2D), 1);
			"A shuffled array of all players within the Gold Mine's radius."
			Global.A = Randomized Array(Global.MinePlayerIntersectionArray2D[Global.ArrayIteratorA]);
			"The maximum harvestable gold."
			Global.B = Global.MineGoldCountArray[Global.ArrayIteratorA];
			For Global Variable(ArrayIteratorB, 0, Count Of(Global.A), 1);
				"The player to process."
				Global.D = Global.A[Global.ArrayIteratorB];
				"The amount of gold to harvest."
				Global.C = Min(Global.B, Global.D.GoldHarvestAmount * Global.TeamGoldHarvestAmountFactorArray[Global.D.TeamIndex]);
				If(Global.C > 0);
					Global.D.GoldCount += Global.C;
					Global.MineGoldCountArray[Global.ArrayIteratorA] -= Global.C;
					Global.SubroutineTargetA = Global.D;
					Call Subroutine(CalculatePlayerSpeedPenalty);
					Play Effect(Global.D, Buff Explosion Sound, Color(White), Position Of(Global.D), 100);
					disabled Log To Inspector(Custom String("Player {0} Harvests {1} Gold from Mine {2}", Global.D, Global.C, Global.ArrayIteratorA));
				End;
			End;
		End;
		Loop;
	}
}

rule("Update@Player: UpdateIsInBase")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Game In Progress == True;
		Is Alive(Event Player) == True;
	}

	actions
	{
		Wait(Global.UpdateInterval, Abort When False);
		"Get EventPlayer's team base location."
		Event Player.B = Global.SpawnPositionTeamArray[Event Player.TeamIndex];
		"Bool indicating whether the event player is in the radius of their base."
		Event Player.C = Distance Between(Event Player.B, Position Of(Event Player))
			<= Global.SpawnRadius + Global.RadiusPlayerProximityBuffer;
		If(Event Player.C && !Event Player.IsInBase);
			Event Player.EnterBaseTimer += Global.UpdateInterval;
			If(Event Player.EnterBaseTimer >= Global.PlayerEnterBaseTimerMax);
				Event Player.EnterBaseTimer = 0;
				Event Player.IsInBase = True;
			End;
		Else If(!Event Player.C && (Event Player.IsInBase || Event Player.EnterBaseTimer > 0));
			"EventPlayer has left the team base; reset the timer."
			Event Player.EnterBaseTimer = 0;
			Event Player.IsInBase = False;
		End;
		Loop If Condition Is True;
	}
}

rule("Update@Player: UpdateGoldLootCollision")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Game In Progress == True;
		Is Alive(Event Player) == True;
		Has Spawned(Event Player) == True;
	}

	actions
	{
		Wait(0.100, Abort When False);
		For Global Variable(ArrayIteratorA, 0, Count Of(Global.GoldLootOrbPositionArray), 1);
			disabled Log To Inspector(Custom String("{0}", Distance Between(Position Of(Event Player),
				Global.GoldLootOrbPositionArray[Global.ArrayIteratorA])));
			If(Distance Between(Position Of(Event Player), Global.GoldLootOrbPositionArray[Global.ArrayIteratorA])
				<= Global.GoldLootOrbCollectionRadius + Global.RadiusPlayerProximityBuffer);
				Destroy Effect(Global.GoldLootOrbEffectArray[Global.ArrayIteratorA]);
				Destroy In-World Text(Global.GoldLootOrbTextArray[Global.ArrayIteratorA]);
				Event Player.GoldCount += Global.GoldLootOrbValueArray[Global.ArrayIteratorA];
				Modify Global Variable(GoldLootOrbPositionArray, Remove From Array By Index, Global.ArrayIteratorA);
				Modify Global Variable(GoldLootOrbValueArray, Remove From Array By Index, Global.ArrayIteratorA);
				Modify Global Variable(GoldLootOrbEffectArray, Remove From Array By Index, Global.ArrayIteratorA);
				Modify Global Variable(GoldLootOrbTextArray, Remove From Array By Index, Global.ArrayIteratorA);
				Play Effect(Event Player, Buff Explosion Sound, Color(White), Event Player, 100);
				Log To Inspector(Custom String("Destroy GoldLoot: {0}", Global.ArrayIteratorA));
			End;
		End;
		Loop If Condition Is True;
	}
}

rule("Update@Global: SpawnGoldLoot")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Wait(0.016, Abort When False);
		"We can't create the Orbs directly in the OnPlayerDeath event, else they get bound to that player and destroyed when the player leaves or swaps teams. This \"worker thread\" moves it into global scope."
		For Global Variable(ArrayIteratorA, 0, Count Of(Global.GoldLootOrbSpawnQueue), 1);
			Modify Global Variable(GoldLootOrbPositionArray, Append To Array, Global.GoldLootOrbSpawnQueue[Global.ArrayIteratorA][0]);
			Modify Global Variable(GoldLootOrbValueArray, Append To Array, Global.GoldLootOrbSpawnQueue[Global.ArrayIteratorA][1]);
			Create Effect(All Players(All Teams), Orb, Global.GoldColor, Global.GoldLootOrbSpawnQueue[Global.ArrayIteratorA][0],
				Global.GoldLootOrbVisualRadius, Visible To);
			Modify Global Variable(GoldLootOrbEffectArray, Append To Array, Last Created Entity);
			Create In-World Text(All Players(All Teams), Custom String("{0}", Global.GoldLootOrbSpawnQueue[Global.ArrayIteratorA][1]),
				Global.GoldLootOrbSpawnQueue[Global.ArrayIteratorA][0], 1, Clip Against Surfaces, Visible To, Global.GoldColor,
				Default Visibility);
			Modify Global Variable(GoldLootOrbTextArray, Append To Array, Last Text ID);
		End;
		Global.GoldLootOrbSpawnQueue = Array();
		Loop If Condition Is True;
	}
}

rule("OnDeath@Player: EnqueueGoldLootDrop")
{
	event
	{
		Player Died;
		All;
		All;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Wait(0.250, Ignore Condition);
		Event Player.A = Round To Integer(Event Player.GoldCount * Global.PlayerDeathGoldDropRatio, Down);
		Abort If(Event Player.A == 0);
		Event Player.B = Array(Up * 0.500 + Evaluate Once(Nearest Walkable Position(Evaluate Once(Position Of(Event Player)))),
			Evaluate Once(Event Player.A));
		"Enqueue a GoldLoot spawn command."
		Global.GoldLootOrbSpawnQueue[Count Of(Global.GoldLootOrbSpawnQueue)] = Event Player.B;
		Event Player.GoldCount -= Event Player.A;
		Global.SubroutineTargetA = Event Player;
		Call Subroutine(CalculatePlayerSpeedPenalty);
	}
}

rule("OnRespawn@Player: TeleportToSpawn")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Has Spawned(Event Player) == True;
		Is Alive(Event Player) == True;
	}

	actions
	{
		Teleport(Event Player, Global.SpawnPositionTeamArray[Event Player.TeamIndex]);
	}
}

rule("OnRespawn@Player: SetIsInBase")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Has Spawned(Event Player) == True;
		Is Alive(Event Player) == True;
	}

	actions
	{
		Event Player.IsInBase = True;
	}
}

rule("OnJoinedMatch@Player: CalculateTeamHandicap")
{
	event
	{
		Player Joined Match;
		All;
		All;
	}

	actions
	{
		Log To Inspector(Custom String("Player {0} joined the match.", Event Player));
		Call Subroutine(CalculateTeamHandicap);
	}
}

rule("OnLeftMatch@Player: CalculateTeamHandicap")
{
	event
	{
		Player Left Match;
		All;
		All;
	}

	actions
	{
		Log To Inspector(Custom String("Player {0} left the match.", Event Player));
		Call Subroutine(CalculateTeamHandicap);
	}
}

rule("OnScoreChanged@Team1: CheckWinCondition")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
		Global.ScoreTeamArray[0] >= Global.ScoreLimit;
	}

	actions
	{
		Declare Team Victory(Team 1);
	}
}

rule("OnScoreChanged@Team2: CheckWinCondition")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
		Global.ScoreTeamArray[1] >= Global.ScoreLimit;
	}

	actions
	{
		Declare Team Victory(Team 2);
	}
}

rule("OnBaseEntered@Player: Shelter")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player.IsInBase == True;
		Is Alive(Event Player) == True;
		Has Spawned(Event Player) == True;
	}

	actions
	{
		Event Player.UltimateCharge = Ultimate Charge Percent(Event Player);
		disabled Set Invisible(Event Player, Enemies);
		Set Status(Event Player, Null, Phased Out, 9999);
		Start Heal Over Time(Event Player, Event Player, 9999, 25);
		Set Ability 1 Enabled(Event Player, False);
		Set Ability 2 Enabled(Event Player, False);
		Set Melee Enabled(Event Player, False);
		Set Ultimate Ability Enabled(Event Player, False);
		Set Primary Fire Enabled(Event Player, False);
		Set Secondary Fire Enabled(Event Player, False);
	}
}

rule("OnBaseLeft@Player: Unshelter")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player.IsInBase == False;
	}

	actions
	{
		Set Invisible(Event Player, None);
		Clear Status(Event Player, Phased Out);
		Stop All Heal Over Time(Event Player);
		Set Ability 1 Enabled(Event Player, True);
		Set Ability 2 Enabled(Event Player, True);
		Set Melee Enabled(Event Player, True);
		Set Ultimate Ability Enabled(Event Player, True);
		Set Primary Fire Enabled(Event Player, True);
		Set Secondary Fire Enabled(Event Player, True);
		Set Ultimate Charge(Event Player, Event Player.UltimateCharge);
	}
}

rule("Update@Player: DepositGoldInBase")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Game In Progress == True;
		Is Alive(Event Player) == True;
		Event Player.IsInBase == True;
		Event Player.GoldCount > 0;
	}

	actions
	{
		disabled Create Effect(Event Player, Energy Sound, Color(White), Eye Position(Event Player), 200, Visible To Position and Radius);
		disabled Event Player.GoldDepositSoundEffectId = Last Created Entity;
		While(True);
			Wait(Global.UpdateInterval, Abort When False);
			Abort If Condition Is False;
			Event Player.A = Min(Global.PlayerGoldDepositPerFrame, Event Player.GoldCount);
			Event Player.GoldCount -= Event Player.A;
			Global.ScoreTeamArray[Event Player.TeamIndex] += Event Player.A;
			Global.SubroutineTargetA = Event Player;
			Call Subroutine(CalculatePlayerSpeedPenalty);
			If(Event Player.GoldCount <= 0);
				Play Effect(Global.D, Buff Explosion Sound, Color(White), Position Of(Global.D), 100);
			End;
		End;
		disabled Destroy Effect(Event Player.GoldDepositSoundEffectId);
	}
}

rule("CrouchInBase@Player: SetTutorialVisible")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player.IsInBase == True;
		Is Crouching(Event Player) == True;
	}

	actions
	{
		Event Player.IsViewingTutorial = True;
		While(True);
			Wait(Global.UpdateInterval, Ignore Condition);
			If(Event Player.IsInBase == False || Is Crouching(Event Player) == False);
				Event Player.IsViewingTutorial = False;
				Abort;
			End;
		End;
	}
}

rule("OnIsViewingTutorialTrue@Player: Show Tutorial")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player.IsViewingTutorial == True;
	}

	actions
	{
		disabled Start Camera(Event Player, Vector(387.834, 2.031, -64.544), Forward, 0);
		Start Camera(Event Player, Vector(327.354, 50, -38.443), Vector(327.354, 50, -38.443) + Forward, 0);
		Disable Hero HUD(Event Player);
	}
}

rule("OnIsViewingTutorialFalse@Player: Hide Tutorial")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player.IsViewingTutorial == False;
	}

	actions
	{
		Stop Camera(Event Player);
		Enable Hero HUD(Event Player);
	}
}

rule("Init@Global:TutorialHUD")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.A = Vector(327.354, 50, -38.443) + Forward * 5;
		Create In-World Text(Filtered Array(All Players(All Teams), Current Array Element.IsViewingTutorial == True), Custom String(
			"Welcome to Rift Raiders!"), Global.A + Up * 2.250, 2, Do Not Clip, Visible To, Color(White), Default Visibility);
		Create In-World Text(Filtered Array(All Players(All Teams), Current Array Element.IsViewingTutorial == True), Custom String(
			"Drain {0} from Rifts around the\r\nbattlefield and return it to your base.\r\nDefeat your foes to steal their {0}!",
			Global.GoldName), Global.A + Up * 1.250, 2, Do Not Clip, Visible To, Color(White), Default Visibility);
		Create In-World Text(Filtered Array(All Players(All Teams), Current Array Element.IsViewingTutorial == True), Custom String(
			"The first team to collect {0} {1} wins.\r\nStand inside the Rifts to mine {1},\r\nthen bring it back to your team's base.",
			Global.ScoreLimit, Global.GoldName), Global.A + Up * 0, 2, Do Not Clip, Visible To, Color(White), Default Visibility);
		Global.B = Global.PlayerDeathGoldDropRatio == 1 ? Custom String("all") : Custom String("{0}% of",
			100 * Global.PlayerDeathGoldDropRatio);
		Create In-World Text(Filtered Array(All Players(All Teams), Current Array Element.IsViewingTutorial == True), Custom String(
			"If you are defeated, you will drop {0}\r\nyour {1}, and others can pick it up!", Global.B, Global.GoldName),
			Global.A + Up * -1, 2, Do Not Clip, Visible To, Color(White), Default Visibility);
		Create In-World Text(Filtered Array(All Players(All Teams), Current Array Element.IsViewingTutorial == True), Custom String(
			"Once depleted, a Rift takes {0} seconds\r\nto recharge before it can be drained again.", Global.MineRespawnDuration),
			Global.A + Up * -2, 2, Do Not Clip, Visible To, Color(White), Default Visibility);
		Create In-World Text(Filtered Array(All Players(All Teams), Current Array Element.IsViewingTutorial == True), Custom String(
			"It takes {0} seconds to enter your base. While\r\nin your base, you are invulnerable, get healed\r\nand can not use any abilities.",
			Global.PlayerEnterBaseTimerMax), Global.A + Up * -3.250, 2, Do Not Clip, Visible To, Color(White), Default Visibility);
	}
}

rule("Subroutine: CalculateTeamHandicap")
{
	event
	{
		Subroutine;
		CalculateTeamHandicap;
	}

	actions
	{
		If(Global.EnableGoldHarvestBalancing == False);
			Global.TeamGoldHarvestAmountFactorArray = Array(1, 1);
			Abort;
		End;
		Global.A = Max(1, Count Of(All Players(Team 1)));
		Global.B = Max(1, Count Of(All Players(Team 2)));
		Global.TeamGoldHarvestAmountFactorArray[0] = Max(1, Global.B / Global.A);
		Global.TeamGoldHarvestAmountFactorArray[1] = Max(1, Global.A / Global.B);
		Log To Inspector(Custom String("Team 1: Players={0}, GoldHarvestFactor={1}", Count Of(All Players(Team 1)),
			Global.TeamGoldHarvestAmountFactorArray[0]));
		Log To Inspector(Custom String("Team 2: Players={0}, GoldHarvestFactor={1}", Count Of(All Players(Team 2)),
			Global.TeamGoldHarvestAmountFactorArray[1]));
	}
}

rule("Subroutine: CalculatePlayerSpeedPenalty")
{
	event
	{
		Subroutine;
		CalculatePlayerSpeedPenalty;
	}

	actions
	{
		Global.SubroutineTargetA.SpeedPenalty = Min(Global.GoldCarrySpeedPenaltyMax, Global.GoldCarrySpeedPenaltyAmount * Round To Integer(
			Max(0, Global.SubroutineTargetA.GoldCount - Global.GoldCarrySpeedPenaltyFreeAmount) / Global.GoldCarrySpeedPenaltyInterval,
			Down));
		Set Move Speed(Global.SubroutineTargetA, 100 - Global.SubroutineTargetA.SpeedPenalty);
	}
}